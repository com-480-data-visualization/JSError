******************************************************************************************************************************************************************************************************************************************************************
****Recodes used to create the most common break variables seen throughout the East Asian Societies report.****
These recodes are used by Pew Research Center to guarantee a large enough sample size within each analyzed subgroup.
Some demographic characteristics (such as age) include multiple recode options.
For analyses using these recodes, researchers looked at the patterns across the different recode options to determine which tells the most coherent and parsimonious story for each of the dependent variables studied.
******************************************************************************************************************************************************************************************************************************************************************


*********************************************************************************************************************************
****Part 1. Recoding variables to create demographic variables used for analysis throughout report (ex. recoding  age into 2 categories: younger (18-34) and older (35+) adults                    
*********************************************************************************************************************************
***** Recoding Age into variables with 2 categories (18-34, 35+), for analysis*****

recode qage (18 thru 34 = 1)(35 thru 97 = 2)(else = copy) into Age2Cat.

*Including those who DK/REF to age into age2cat for Hong Kong, South Korea and Vietnam*

if (SurveyPublic =1) & (QAGE_NRHKG=1 or QAGE_NRHKG=2) Age2Cat=1.
if (SurveyPublic =1) & (QAGE_NRHKG=3 or QAGE_NRHKG=4 or QAGE_NRHKG=5 or QAGE_NRHKG=6) Age2Cat=2.
if (SurveyPublic=4 ) & (QAGE_NRKOR=1 or QAGE_NRKOR=2) Age2Cat=1.
if (SurveyPublic=4 ) & (QAGE_NRKOR=3 or QAGE_NRKOR=4 or QAGE_NRKOR=5 or QAGE_NRKOR=6) Age2Cat=2.
if (SurveyPublic =6) & (QAGE_NRVNM=1 or QAGE_NRVNM=2) Age2Cat=1.
if (SurveyPublic =6) & (QAGE_NRVNM=3 or QAGE_NRVNM=4 or QAGE_NRVNM=5 or QAGE_NRVNM=6) Age2Cat=2.

*Including those who DK/REF into age2cat for Japan, Taiwan. Because the second age group classification overlaps with both Age2Cat categories, those few cases have been excluded from this recode.*

if (SurveyPublic=2) & (QAGE_NRJPN=1) Age2Cat=1.
if (SurveyPublic=2) & (QAGE_NRJPN=3 or QAGE_NRJPN=4 or QAGE_NRJPN=5) Age2Cat=2.
if (SurveyPublic=5) & (QAGE_NRTWNrec=1) Age2Cat=1.
if (SurveyPublic=5) & (QAGE_NRTWNrec=3 or QAGE_NRTWNrec=4 or QAGE_NRTWNrec=5) Age2Cat=2.
variable labels age2cat 'Age2Cat. Age in two categories (18-34/35+)'
value labels age2cat 1 '18-34' 2 '35+' 998 "Don't know" 999 'Refused'.

***** Recoding Age into variables with 2 categories (18-59, 60+), for analysis*****

recode qage (18 thru  59= 1)(60 thru 97 = 2)(else = copy) into Age2CatAlt.

*Including those who DK/REF to age into age2catalt for Hong Kong, South Korea and Vietnam. Because the fifth age group classification overlaps with both age2catalt categories, those few cases have been excluded from this recode.*

if (SurveyPublic =1) & (QAGE_NRHKG=1 or QAGE_NRHKG=2 or QAGE_NRHKG=3 or QAGE_NRHKG=4) Age2CatAlt=1.
if (SurveyPublic =1) & (QAGE_NRHKG=6) Age2CatAlt=2.
if (SurveyPublic=4 ) & (QAGE_NRKOR=1 or QAGE_NRKOR=2 or QAGE_NRKOR=3 or QAGE_NRKOR=4) Age2CatAlt=1.
if (SurveyPublic=4 ) & (QAGE_NRKOR=6) Age2CatAlt=2.
if (SurveyPublic =6) & (QAGE_NRVNM=1 or QAGE_NRVNM=2 or QAGE_NRVNM=3 or QAGE_NRVNM=4) Age2CatAlt=1.
if (SurveyPublic =6) & (QAGE_NRVNM=6) Age2CatAlt=2.

*Including those who DK/REF into age2catalt for Japan, Taiwan.**

if (SurveyPublic=2) & (QAGE_NRJPN=1 or QAGE_NRJPN=2 or QAGE_NRJPN=3) Age2CatAlt=1.
if (SurveyPublic=2) & (QAGE_NRJPN=4 or QAGE_NRJPN=5) Age2CatAlt=2.
if (SurveyPublic=5) & (QAGE_NRTWNrec=1 or QAGE_NRTWNrec=2 or QAGE_NRTWNrec=3 ) Age2CatAlt=1.
if (SurveyPublic=5) & (QAGE_NRTWNrec=4 or QAGE_NRTWNrec=5) Age2CatAlt=2.

variable labels Age2CatAlt 'Age2CatAlt. Age in two categories (18-59/60+)'.
value labels Age2CatAlt 1 '18-59' 2 '60+' 998 "Don't know" 999 'Refused'.

***** Recoding Religious Salience (Q9. How important is religion?) into those who say it's very important and everyone else, for analysis*****

recode q9 (1=1)(2 thru 4 = 2)(else=copy) into Q9Rec.
variable labels Q9Rec 'Q9Rec. Salience recoded in two categories [Very important vs Else]'.
value labels Q9Rec 1 'Religion very important' 2 'Religion less important' 98 "Don't know" 99 'Refused'.

recode q9 (1 thru 2=1)(3 thru 4 = 2)(else=copy) into Q9Rec2.
variable labels Q9Rec2 'Q9Rec2. Salience recoded in two categories [Important/Not]'.
value labels Q9Rec2 1 'NET Religion important' 2 'NET Religion not important' 98 "Don't know" 99 'Refused'.

***** Recoding Frequency of Prayer into two categories: those who pray daily and everyone else, for analysis*****

recode Q28 (1 thru 2 =1) (3 thru 7 = 2) (else = copy) into PrayDaily.
variable labels PrayDaily 'PrayDaily. Prayer in two categories'.
value Labels PrayDaily 1 'Pray Daily' 2 'Pray Less than Daily' 98 'DK' 99 'Refused'.


***** Recoding Education into higher/lower categories. We used ISCED classification and,
       for the purpose of comparing education groups across countries, we standardize education 
       levels based on the UN's International Standard Classification of Education*****

compute Edu2Cat = $sysmis.

if (SurveyPublic=1 or SurveyPublic=2 or SurveyPublic=4 or SurveyPublic=5) & (isced =0 or isced =1 or isced = 2 or isced=3) edu2cat =1.
if (SurveyPublic=1 or SurveyPublic=2 or SurveyPublic=4 or SurveyPublic=5) & (isced =4 or isced = 5) edu2cat =2.
if (SurveyPublic=6) & (isced =0 or isced =1 or isced = 2 ) edu2cat =1.
if (SurveyPublic=6) & (isced=3 or isced =4 or isced = 5) edu2cat =2.
if (isced = 98 or isced=99) edu2cat= 999.

variable labels edu2cat 'Edu2Cat. Education in two categories (Lower Education/Higher Education)'.
value labels edu2cat 1 'Lower Education' 2 'Higher Education' 999 "DK/ Refused".

****Gender recode variable which combines the interviewer-observed with the self-ID, where available.****
****Note: the .sav and .csv files already contain this recoded 'GenderRec' variable; as such, the variables 'qgen' and 'qgenb' are not in the .sav and .csv files. The syntax below is included to show how the 'GenderRec' variable was created. ****

compute GenderRec=$sysmis.
if (SurveyPublic=1 or SurveyPublic=2 or SurveyPublic=4 or SurveyPublic=5) GenderRec = qgenb.
if (SurveyPublic =6) GenderRec = qgen.
if (SurveyPublic =6 & qgen=98) GenderRec =qgenb.
if (gender=3 or gender = 98 or gender=99) GenderRec = 999.

variable labels GenderRec 'Gender'.
value labels GenderRec 1 'Man' 2 'Woman' 999 'Other/DK/Ref'


*********************************************************************************************************************************
****Part 2. Recoding variables for  specific questions and chapters analysis  ******                       
*********************************************************************************************************************************

************************************
****Overview Recodes******                       
************************************

****Recoding q10 and q11 into two categories: those who believe in god and/or unseen beings and those who believe in neither, for analysis. ****

compute GodUnseen=0.
if q10=1 GodUnseen=1.
if q11=1 GodUnseen=1.
value labels GodUnseen 0 'believes in neither' 1 'believes in god and/or unseen beings'.

****Recoding q24c and q24d into two categories: those who say ancestors visit in a dream or in any other way, and those who say neither, for analysis. ****

compute anyvisit=0.
if q24c=1 anyvisit=1.
if q24d=1 anyvisit=1.
value labels anyvisit 0 'no visits' 1 'visits in any way'.

****combining childhood religion and current religion to see respondents fall into any of three categories: lifelong Buddhists, switchers from Buddhism to no religion, and people who have never had a religion****

if qchrel=1 & qcurrel=1 OverviewGroups=1.
if qchrel=1 & qcurrel=8 OverviewGroups=2.
if qchrel=8 & qcurrel=8 OverviewGroups=3.
Value labels  OverviewGroups 1 "Lifelong Buddhist" 2 "Buddhist to None" 3 "Lifelong None".

************************************
****Chapter 2 Recodes******                       
************************************

****Recoding the main religion variable to create Buddhist, Christian and unaffiliated dummy variables****

recode qcurrel (1=1)(else = 2) into Buddhist.
value labels Buddhist 1 'Buddhist' 2 'Not'.
recode qcurrel (8=1)(else=2) into Unaffiliated.
value labels unaffiliated 1 'No religion' 2 'Other'.
recode qcurrel (2=1)(else=2) into Christian.
value labels christian 1 'Christan' 2 'Not'.

****For this chapter, we need to create a scale for how many religions BESIDES THEIR OWN respondents feel a personal connection towards. This starts by recoding the battery items for Q6 (“Do you feel a personal connection to each of the following ways of life, or not?) into dummy variables. NOTE: These recodes often use the terms “affinity” and “personal connection” interchangeably****

Recode Q6a (1=1) (else=0) into Q6aCh2.
Recode Q6b (1=1) (else=0) into Q6bCh2.
Recode Q6c (1=1) (else=0) into Q6cCh2.
Recode Q6d (1=1) (else=0) into Q6dCh2.
Recode Q6e (1=1) (else=0) into Q6eCh2.
Recode Q6f (1=1) (else=0) into Q6fCh2.

****Computes a variable to measure how many people said that they have a personal connection to each religion (NOTE: Muslims and those without a religious affiliation were asked all battery items.)****

compute totaff=0.
if qcurrel=1 totaff=Q6bCh2+Q6cCh2+Q6dCh2+Q6eCh2+Q6fCh2. /* Buddhist No A.
if qcurrel=2 totaff=Q6aCh2+Q6cCh2+Q6dCh2+Q6eCh2+Q6fCh2. /* Christian No B.
if qcurrel=4 totaff=Q6aCh2+Q6bCh2+Q6dCh2+Q6eCh2+Q6fCh2. /* Shinto No C.
if qcurrel=5 totaff=Q6aCh2+Q6bCh2+Q6cCh2+Q6eCh2+Q6fCh2. /* Daoist No D.
if qcurrel=6 totaff=Q6aCh2+Q6bCh2+Q6cCh2+Q6dCh2+Q6fCh2. /* Confucian No E.
if qcurrel=7 totaff=Q6aCh2+Q6bCh2+Q6cCh2+Q6dCh2+Q6eCh2. /*Local No F.
if (qcurrel = 3) or (qcurrel>7) totaff=Q6aCh2+Q6bCh2+Q6cCh2+Q6dCh2+Q6eCh2+Q6fCh2.
**pulls out those who have affinity to their own religion.
if qcurrel=1 & totaff=1 & Q6aCh2=1 totaff=88.
if qcurrel=2 & totaff=1 & Q6bCh2=1 totaff=88.
if qcurrel=4 & totaff=1 & Q6cCh2=1 totaff=88.
if qcurrel=5 & totaff=1 & Q6dCh2=1 totaff=88.
if qcurrel=6 & totaff=1 & Q6eCh2=1  totaff=88.
if qcurrel=7 & totaff=1 & Q6fCh2=1 totaff=88.
variable labels totaff 'Affinity NOT your religion'.

****recodes totaff to show those who have affinity to no other religions, 1-2 other religions and 3 or more religions.****

compute totaff2=0.
if totaff = 1 or totaff = 2 totaff2=1.
if totaff>2 totaff2=2.
value labels totaff2 0 'No other religions' 1 'One or two other religion' 2 '3+'.

****Creating a large variable out of the Q7 battery to allow for any combination of responses****

compute q7rec = 999.
if q7a = 1 and q7b = 2 and q7c = 2 and q7d = 2 and q7e = 2 q7rec = 1.
if q7a = 2 and q7b = 1 and q7c = 2 and q7d = 2 and q7e = 2 q7rec = 2.
if q7a = 2 and q7b = 2 and q7c = 1 and q7d = 2 and q7e = 2 q7rec = 3.
if q7a = 2 and q7b = 2 and q7c = 2 and q7d = 1 and q7e = 2 q7rec = 4.
if q7a = 2 and q7b = 2 and q7c = 2 and q7d = 2 and q7e = 1 q7rec = 5.
if q7a = 1 and q7b = 1 q7rec = 6.
if q7a = 1 and q7c = 1 q7rec = 7.
if q7a = 1 and q7d = 1 q7rec = 8.
if q7a = 1 and q7e = 1 q7rec = 9.
if q7b = 1 and q7c = 1 q7rec = 10.
if q7b = 1 and q7d = 1 q7rec = 11.
if q7b = 1 and q7e = 1 q7rec = 12.
if q7c = 1 and q7d = 1 q7rec = 13.
if q7c = 1 and q7e = 1 q7rec = 14.
if q7d = 1 and q7e = 1 q7rec = 15.
if q7a = 1 and q7b = 1 and q7c = 1 q7rec = 16.
if q7a = 1 and q7b = 1 and q7d = 1 q7rec = 17.
if q7a = 1 and q7b = 1 and q7e = 1 q7rec = 18.
if q7a = 1 and q7c = 1 and q7d = 1 q7rec = 19.
if q7a = 1 and q7c = 1 and q7e = 1 q7rec = 20.
if q7a = 1 and q7d = 1 and q7e = 1 q7rec = 21.
if q7b = 1 and q7c = 1 and q7d = 1 q7rec = 22.
if q7b = 1 and q7c = 1 and q7e = 1 q7rec = 23.
if q7b = 1 and q7d = 1 and q7e = 1 q7rec = 24.
if q7c = 1 and q7d = 1 and q7e = 1 q7rec = 25. 
if q7a = 1 and q7b = 1 and q7c = 1 and q7d = 1 q7rec = 26.
if q7a = 1 and q7b = 1 and q7c = 1 and q7e = 1 q7rec = 27.
if q7a = 1 and q7b = 1 and q7d = 1 and q7e = 1 q7rec = 28.
if q7a = 1 and q7c = 1 and q7d = 1 and q7e = 1 q7rec = 29.
if q7b = 1 and q7c = 1 and q7d = 1 and q7e = 1 q7rec = 30.
if q7a = 1 and q7b = 1 and q7c = 1 and q7d = 1 and q7e = 1 q7rec = 31.
if q7a = 2 and q7b = 2 and q7c = 2 and q7d = 2 and q7e = 2 q7rec = 32.

value labels q7rec
1 'Just family tradition'
2 'Just ethnicity'
3 'Just culture'
4 'Just an ethical teaching'
5 'Just religion you choose'
6 'Tradition and ethnicity'
7 'Tradition and culture'
8 'Tradition and ethics'
9 'Tradition and religion'
10 'Ethnicity and culture'
11 'Ethnicity and ethics'
12 'Ethnicity and religion'
13 'Culture and ethics'
14 'Culture and religion'
15 'Ethics and religion'
16 'Tradition, ethnicity and culture'
17 'Tradition, ethnicity and ethics'
18 'Tradition, ethnicity and religion'
19 'Tradition, culture and ethics'
20 'Tradition, culture and religion'
21 'Tradition, ethics and religion'
22 'Ethnicity, culture and ethics'
23 'Ethnicity, culture and religion'
24 'Ethnicity, ethics and religion'
25 'Culture, ethics and religion'
26 'Tradition, ethnicity, culture and ethics'
27 'Tradition, ethnicity, culture and religion'
28 'Tradition, ethnicity, ethics and religion'
29 'Tradition, culture, ethics and religion'
30 'Ethnicity, culture, ethics and religion'
31 'All five'
32 'None'.

****recode this newly recoded variable to highlight people who said that their religion was either all five or none of these options****

recode q7rec (31 = 1)(else = 2) into allfive.
recode q7rec (31 = 1)(32 = 2)(else = 3) into allornone.
value labels allornone 1 ‘All five’ 2 ‘None’ 3 ‘Else’.

****recoding the Q6 battery items into dummy variables for additional analyses****

recode q6a (1=1)(else=2) into buddaff.
value labels buddaff 1 'Buddhist affinity' 2 'No Buddhist affinity'.
recode q6b (1=1)(else=2) into chrisaff.
value labels chrisaff 1 'Christian affinity' 2 'No Christian affinity'.
recode q6f (1=1)(else=2) into localaff.
value labels localaff 1 'Affinity with local religions' 2 'No affinity with local religions'.
recode q6e (1=1)(else=2) into confuaff.
value labels confuaff 1 'Affinity with Confucianism' 2 'No Confucian affinity'.
recode q6c (1=1)(else=2) into shintoaff.
value labels shintoaff 1 'Shinto affinity' 2 'No Shinto affinity'.

************************************
****Chapter 3 Recodes******                       
************************************

**Creates dummy variables for belief in unseen beings/god.**

recode q11 (1=1)(else=0) into ubbel.
recode q10 (1=1)(else=0) into godbel.

**Uses those dummy variables to run a paired samples t-test by location/religion to test significance

weight by weight.
temporary.
select if  SurveyPublic=1.
T-Test Pairs=ubbel WITH godbel (PAIRED)
    /CRITERIA=CI(.95)
    /MISSING=ANALYSIS.

************************************
****Chapter 4 Recodes******                       
************************************

****Recoding prayer frequency into four categories, for analysis****
compute prayfreq=$sysmis.
if q28=1 OR q28=2 prayfreq=1.
if q28=3 OR q28=4 prayfreq=2.
if q28=5 OR q28=6 prayfreq=3.
if q28=7 prayfreq=4.
if q28=98 OR q28=99 prayfreq=5.
value labels prayfreq 1 'At least daily' 2 'Weekly' 3 'Monthly/Less often' 4 'Never' 5 'DK/Ref'.

****Recoding church attendance frequency into three categories, for analysis****
compute attendrec=$sysmis.
if qattend=1 OR qattend=2 OR qattend=3 attendrec=1.
if qattend=4 OR qattend=5 OR qattend=6 attendrec=2.
if qattend=7 attendrec=3.
if qattend=98 OR qattend=99 attendrec=4.
value labels attendrec 1 'At least weekly' 2 'Less often' 3 'Never' 4 'DK/Ref'.

****Recoding Q26b and Q26a into three categories, for analysis****
compute meaning3cat=$sysmis.
if q26b=1 or q26b=2 meaning3cat=1.
if q26b=3 or q26b=4 meaning3cat=2.
if q26b=5 meaning3cat=3.
if q26b=98 or q26b=99 or q26b=999 meaning3cat=4.
value labels meaning3cat 1 'At least monthly' 2 'Less often' 3 'Never' 4 'DK/Ref'.

compute wonder3cat=$sysmis.
if q26a=1 or q26a=2 wonder3cat=1.
if q26a=3 or q26a=4 wonder3cat=2.
if Q26a=5 wonder3cat=3.
if q26a=98 or q26a=99 or q26a=999 wonder3cat=4.
value labels wonder3cat 1 'At least monthly' 2 'Less often' 3 'Never' 4 'DK/Ref'.


************************************
****Chapter 5 Recodes******                       
************************************

****Merging Q16_1 and Q16_b  so the new variables has 3 categories: Only cremates, only buries, or both .****

compute CremateOrBury = 0.
if q16_1 = 1 and q16_2 = 2 CremateOrBury = 1.
if q16_1 = 2 and q16_2 =1 CremateOrBury = 2.
if q16_1 = 1 and q16_2 =1 CremateOrBury = 3.
Value labels CremateOrBury 1 "Cremates, doesn't bury" 2 "Buries, doesn't cremate" 3 'Both'.



*********************************************************************************************************************************
****Part 3. Recoding variables to match how they appear in the topline ******                       

****Merging Q19 and Q20a so the resulting variable appears as in the topline 
        A modified version of this recode appears in Chapter 5 of the report.****

recode q20a (1=1) (2=2) (98, 99, 999 = 3) into q20arec.
recode q19 (2=4) (98, 99, 999 = 5) into q20arec.

Add Value Labels q20arec 1 'Has a family gravesite, pays money to maintain it' 2 'Has a family gravesite, does not pay money to maintain it' 3 'Has a family gravesite, DK/ refused if pays money to maintain it' 4 'Does not have a family gravesite' 5 'DK/ Refused if has a family gravesite'.
Variable Labels q20arec 'q20arec. Do you have a family gravesite where the ashes or remains of deceased family members reside? [If yes] Do you or someone in your household pay money to maintain the family gravesite, or not?'.

****Merging Q19 and Q20b so the resulting variable appears as in the topline 
        A modified version of this recode appears in Chapter 5 of the report.****

recode q20b (1=1) (2=2) (98, 99, 999 = 3) into q20brec.
recode q19 (2=4) (98, 99, 999 = 5) into q20brec.

Add Value Labels q20brec 1 'Has a family gravesite, looks after it by sweeping or cleaning' 2 'Has a family gravesite, does not look after it by sweeping or cleaning' 3 'Has a family gravesite, DK/ refused if looks after it by sweeping or cleaning' 4 'Does not have a family gravesite' 5 'DK/ Refused if has a family gravesite'.
Variable Labels q20brec 'q20brec. Do you have a family gravesite where the ashes or remains of deceased family members reside? [If yes] Do you or someone in your household look after the family gravesite by sweeping or cleaning it?'.


****Merging Q19 and Q21 so the resulting variable appears as in the topline 
        A modified version of this recode appears in Chapter 5 of the report.****

recode q21 (1=1) (2=2) (3=3) (4=4) (5=5) (98, 99, 999 =6) into q21rec.
recode q19 (2=7) (98, 99, 999 = 8) into q21rec.

Add Value Labels q21rec 1 'Has a family gravesite, visits at least once a month' 2 'Has a family gravesite, visits a few times a year' 3 'Has a family gravesite, visits once a year' 4 'Has a family gravesite, visits less often than that [OR]' 5 'Has a family gravesite, never visits' 6 'Has a family gravesite, DK/ refused how often visits' 7 'Does not have a family gravesite' 8 'DK/ Refused if has a family gravesite'.
Variable Labels q21rec 'q21rec. Do you have a family gravesite where the ashes or remains of deceased family members reside? [If yes] How often do you visit the family gravesite, if at all?'.


****Merging Q16_1 and Q17 so the resulting variable appears as in the topline 
        A modified version of this recode appears in Chapter 5 of the report.****

recode q17 (else=copy) into q17rec.
recode q16_1 (2=9999) into q17rec.

Add value labels Q17rec 1 'Scatter the ashes' 2 'Keep them at a particular location' 3 '(DO NOT READ) Some other method/ Both' 9999 '[Does not cremate]'.
Variable labels Q17rec 'Q17rec. Does your family scatter the ashes of deceased family members OR do you keep them at a particular location, like someone’s home, a temple or pagoda, or a gravesite?'.
